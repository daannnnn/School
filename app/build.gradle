apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.mikepenz.aboutlibraries.plugin'
apply plugin: 'com.google.gms.google-services'

android {
    signingConfigs {
        release {
            Properties properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())
            storeFile file(properties.getProperty('storeFile'))
            keyAlias properties.getProperty('keyAlias')
            storePassword properties.getProperty('storePassword')
            keyPassword properties.getProperty('keyPassword')
        }
    }
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.dan.school"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 6
        versionName "2.0.2"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.incremental": "true"]
            }
        }
    }

    def adMobAdUnitIdFile = rootProject.file("adMobAdUnitId.properties")
    def adMobAdUnitIdProperties = new Properties()
    adMobAdUnitIdProperties.load(new FileInputStream(adMobAdUnitIdFile))

    buildTypes {
        debug {
            resValue("string", "bannerCompletedScreenId", "ca-app-pub-3940256099942544/6300978111")
            resValue("string", "bannerItemId", "ca-app-pub-3940256099942544/6300978111")
            resValue("string", "interstitial1Id", "ca-app-pub-3940256099942544/1033173712")
            resValue("string", "appId", adMobAdUnitIdProperties.getProperty("appId"))
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            resValue("string", "bannerCompletedScreenId", adMobAdUnitIdProperties.getProperty("bannerCompletedScreen"))
            resValue("string", "bannerItemId", adMobAdUnitIdProperties.getProperty("bannerItem"))
            resValue("string", "interstitial1Id", adMobAdUnitIdProperties.getProperty("interstitial1"))
            resValue("string", "appId", adMobAdUnitIdProperties.getProperty("appId"))
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

aboutLibraries {
    configPath = "app/src/main/res/raw"
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.3.0-rc01'
    implementation 'com.github.antonyt:InfiniteViewPager:v1.0.0'
    implementation 'androidx.room:room-runtime:2.2.6'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'androidx.activity:activity-ktx:1.1.0'
    implementation 'androidx.fragment:fragment-ktx:1.2.5'
    implementation 'com.github.kizitonwose:CalendarView:0.4.2'
    implementation 'androidx.room:room-ktx:2.2.6'
    implementation 'com.mikepenz:aboutlibraries:8.6.8'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.2'
    implementation group: 'net.lingala.zip4j', name: 'zip4j', version: '2.6.4'
    implementation group: 'commons-io', name: 'commons-io', version: '2.4'

    implementation platform('com.google.firebase:firebase-bom:26.2.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-auth-ktx'
    implementation 'com.google.firebase:firebase-database-ktx'
    implementation 'com.google.firebase:firebase-storage-ktx'
    implementation 'com.google.firebase:firebase-messaging-ktx'
    implementation 'com.google.android.gms:play-services-auth:19.0.0'
    implementation 'com.google.android.gms:play-services-ads:19.7.0'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.1'
    kapt 'androidx.room:room-compiler:2.2.6'
    testImplementation 'junit:junit:4.13.1'
    testImplementation "com.google.truth:truth:1.0.1"
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation "com.google.truth:truth:1.0.1"
}